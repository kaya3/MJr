# Translated from Maxim Gumin's 'NystromDungeon' model, which
# is based on Bob Nystrom's dungeon generation algorithm
# https://github.com/mxgmn/MarkovJunior/blob/main/models/NystromDungeon.xml
# http://journal.stuffwithstuff.com/2014/12/21/rooms-and-mazes/

let NUM_CYCLES = 5

grid [BPWRG]

# draw a grid
put [P] at origin
all: [PBB] -> [..P]

let roomIn = [
    PBPBPBPBP /
    BBBBBBBBB /
    PBPBPBPBP /
    BBBBBBBBB /
    PBPBPBPBP /
    BBBBBBBBB /
    PBPBPBPBP
]
let roomOut = [
    WWWWWWWWW /
    WWWWWWWWW /
    WWWWWWWWW /
    WWWWWWWWW /
    WWWWWWWWW /
    WWWWWWWWW /
    WWWWWWWWW
]

one: roomIn -> roomOut

# generate corridors with MazeBacktracker
markov:
    one: [RBP] -> [GGR]
    one: [GGR] -> [RWW]
    one: [P] -> [R]

# leave only one start
once: [R] -> [G]

# forget other starts
all: [R] -> [W]

# connect components
markov:
    all: [GWW] -> [..G]
    one: [GBW] -> [.WG]

# insert cycles
@limit NUM_CYCLES
one: [GBW] -> [.W.]

# forget structure
all: [G] -> [W]

# retract dead ends
all: [BBB/BWB] -> [BBB/BBB]
